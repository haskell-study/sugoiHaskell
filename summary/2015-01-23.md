# 4 Hello 再帰!
## 4.1 最高に最高
リストの中で最大のものを返す`maximum'`関数を再帰(recursion)的に定義してみます。
```haskell
maximum' :: (Ord a) => [a] -> a
maximum' [] = error "maximum of empty list!"
maximum' [x] = x
maximum' (x:xs) = max x (maximum' xs)
```
この再帰は`maximum' [x] = x`という**基底部**まで分解されます。

## 4.2 さらにいくつかの再帰関数
すでに存在する関数を再帰に慣れるために一から実装してみます。
### replicate
値を指定された数だけ繰り返したリストを返す関数
```haskell
replicate' :: Int -> a -> [a]
replicate' n x
  | n <= 0    = []
  | otherwise = x : replicate' (n-1) x
```
これも基底部があります。
### take
与えられたリストから指定された数の要素(先頭から連続)を返す関数
```haskell
take' :: Int -> [a] -> [a]
take' n _
  | n <= 0      = []
take' _ []      = []
take' n (x:xs)  = x : take' (n-1) xs
```
今回は基底部が二つあります。
### reverse
与えられたリストを逆順にして返す関数
```haskell
reverse' :: [a] -> [a]
reverse' [] = []
reverse' (x:xs) = reverse' xs ++ [x]
```
基底部があります。
### repeat
与えられた要素からなる無限リストを返す関数
```haskell
repeat' :: a -> [a]
repeat' x = x : repeat' x
```
これは基底部の**ない**再帰関数です。
### zip
二つのリストをとじ合わせます。  
例えば`[1,2,3,4,5]`と`[4,3,2,1]`がこの順番で与えられた場合、`[(1,4),(2,3),(3,2),(4,1)]`が返されます。
```haskell
zip' :: [a] -> [b] -> [(a,b)]
zip' _ []           = []
zip' [] _           = []
zip' (x:xs) (y:ys)  = (x,y) : zip' xs ys
```
基底部が二つあります。  
見てわかるように、入力の二つのリストの長さが異なる場合は長い方の残りの部分が無視されます。
### elem
値とリストが与えられて、その値がリストの中に存在するかを返します。  
elementの略かな
```haskell
elem' :: (Eq a) => a -> [a] -> Bool
elem' a [] = False
elem' a (x:xs)
  | a == x    = True
  | otherwise = a `elem'` xs
```
基底部が一つ存在します。Boolの値が何も手を加えられずに初めの関数まで戻ってくる感じです。
## 4.3 クイック、ソート！
アルゴリズムはみなさんよくご存知だとおもいますので省略します。  
Ordのインスタンスからなるリストを受け取り、昇順にして返す関数を実際に作ってみます。
```haskell
quicksort :: (Ord a) => [a] -> [a]
quicksort [] = []
quicksort (x:xs) = 
  let smallerOrEqual = [a | a <- xs, a <= x]
      larger = [a | a <- xs, a > x]
  in  quicksort smallerOrEqual ++ [x] ++ quicksort larger
```
空リストが基底部になっています。

## 4.4 再帰的に考える
再帰には定跡があります。  

1. 自明な基底部から考えること
2. 部分問題に分解すること
3. 部分問題から全体の解を構築すること

全体として何が起こるかの詳細を考える必要は**ない**のです。

# 5 高階関数
引数、返り値に関数を用いることができる関数を**高階関数**といいます。
## 5.1 カリー化関数
Haskellの全ての関数は引数を1つだけとることになっています。複数の引数をとっているようにみえる関数は**カリー化**された関数です。`max`関数を例に考えます。
```sh
ghci> :t max
max :: (Ord a) => a -> a -> a
```
ですが、これは
```sh
max :: (Ord a) => a -> (a -> a)
```
とも書けます(``->``はデフォルトで右結合なのでこのように明示しなくても良い)。型シグネチャ(上の`:t`ででてくるやつ)に``->``を含むものは全て関数なので、`max`関数の場合は、a型の値を受け取って「a型の値を受け取ってa型の値を返す関数」を返す関数、であることがわかります。実際に
```sh
ghci> :t max 2
max 2 :: (Num a, Ord a) => a -> a
```
により`max 2`関数は「a型の値を受け取ってa型の値を返す関数」であることが確認されます。  
次に
```haskell
multThree :: Int -> (Int -> (Int -> Int))
multThree x y z = x * y * z
```
を用いて**部分適用**を学びます。部分適用された関数とは関数を本来より少ない引数で呼び出した時に返される関数のことです。
```sh
ghci> let multTwoWithNine = multThree 9
ghci> multTwoWithNine 2 3
54
```
このように部分適用された関数を用いて新しい関数を作ることができます。  
中置関数に対しても部分適用が可能です。それは**セクション**という機能を用いることで実現されます。具体的には、中置関数とその片側だけに値を置き全体を括弧でかこみます。  
例:
```haskell
divideByTne :: (Floating a) => a -> a
divideByTne = (/10)

isUpperAlphanum :: Char -> Bool
isUpperAlphanum = (`elem` ['A' .. 'Z'])
```
`-`は注意が必要で、たとえば`(-4)`とセクションしたつもりでも、これはマイナス4を意味してしまいます。このような時は`(subtract 4)`とします。
部分適用された関数を引数に渡すことで面白い関数がつくれます。(次の5.2 高階実演)  
**カリー化関数と部分適用は本当に重要であるので、完全に理解する**
## 5.2 高階実演
高階関数の実演としてある関数`f`を受け取り`f`を二回適用する高階関数`applyTwice`を書いてみます。
```haskell
applyTwice :: (a -> a) -> a -> a
applyTwice f x = f (f x)
```
この型宣言にある`()`は必要です。``->``は右結合なので`()`を省略してしまうと``applyTwice :: a -> (a -> (a -> a))``と解釈されます。しかし一つ目の引数`f`は関数を想定しているのでエラーとなります。つまり今回の場合、`f`の型は`a -> a`を期待しています。  
実際に`applyTwice`を動かしてみます。
```sh
ghci> applyTwice (+3) 10
16
ghci> applyTwice (++ " HAHA") "HEY"
"HEY HAHA HAHA"
ghci> applyTwice ("HAHA " ++) "HEY"
"HAHA HAHA HEY"
ghci> applyTwice (multThree 2 2) 9
144
ghci> applyTwice (3:) [1]
[3, 3, 1]
```
次に二つの高階関数を実装します。
### zipWith
標準ライブラリにあるzipWithを実装してみます。関数と二つのリストを引数にとり、二つのリストの各要素にその関数を適用して一つのリストに結合します。
```haskell
zipWith' :: (a -> b -> c) -> [a] -> [b] -> [c]
zipWith' _ [] _ = []
zipWith' _ _ [] = []
zipWith' f (x:xs) (y:ys) = f x y : zipWith' f xs ys
```
この関数は2015-01-15のゼミでyudaitnb君が出題問題中で利用したので知っていますね。
### flip
こちらも標準ライブラリに含まれています。関数を一つ受け取り、二つの引数が入れ替わった関数を返します。
```haskell
flip' :: (a -> b -> c) -> (b -> a -> c)
flip' f = g
  where g x y = f y x
```
これは次のようにも書けます。
```haskell
flip' :: (a -> b -> c) -> (b -> a -> c)
flip' f x y = f y x
```
この場合は`flip' f`としたときに意味のある部分適用にすることができます。
