# 12章 モノイド
## 12.1 既存の型を新しい型にくるむ
`ZipList a`型のデータ宣言として
```haskell
data ZipList a = ZipList [a]
```
あるいは
```haskell
data ZipList a = ZipList {getZipList :: [a]}
```
が考えられるが、これは**newtype**キーワードを用いて
```haskell
newtype ZipList a = ZipList {getZipList :: [a]}
```
のようにも書ける。「1つの既存型を取り、それを何かに包んで別の型に見せかけたい」という場合に`newtype`によるデータ宣言を行う。`newtype`により新しい型を作ると`data`で同じことをした場合より高速に動作するが、宣言時に値コンストラクタは1種類しか作れず、そのフィールドも1つだけという制限がある。

### newtypeを使って型クラスのインスタンスを作る
タプルをFunctorのインスタンスにすることを考える。ただし`fmap`が作用するのは第一要素(fst)であるとする。
```haskell
newtype Pair b a = Pair {getPair :: (a, b)}

instance Functor (Pair c) where
    fmap f (Pair (x, y)) = Pair (f x, y)
```
newtypeで要素の順番が逆な`Pair`をつくることでインスタンス化を可能にしている。  
newtypeはもっぱら型クラスのインスタンスを作りやすくするために使われる。

### newtypeと遅延評価
```haskell
data CoolBool = CoolBool { getCoolBool :: Bool }
```
と宣言した時と
```haskell
newtype CoolBool = CoolBool { getCoolBool :: Bool } 
```
と宣言した時とでは、例えば関数
```haskell
helloMe :: CoolBool -> String
helloMe (CoolBool _) = "hello"
```
に対し、`undefined`を渡した時の結果が異なる。  
これはnewtypeに存在する制限(値コンストラクタは1つ、フィールドも1つ)により起こる。`newtype`であれば、引数のパターンマッチの際にどの値コンストラクタが使われたのか評価する必要がない。(初めから``(CoolBool _)``パターンに合致するということがわかっている。)

### type vs. newtype vs. data

* 型シグネチャを整理したいとか、型名が体を表すようにしたいだけなら、たぶん型シノニムを使うといいでしょう。
* 既存の型をある型クラスのインスタンスにしたくて、新しい型に包む方法をさがしているのなら、**newtype**がぴったり!
* 何か全く新しいものを作りたい人には、きっと**data**が向いているよ!

## 12.2 Monoid大集合

* 関数は引数を2つとる
* 2つの引数および返り値の型は全て等しい
* 2引数関数を施して相手を変えないような特殊な値が存在する
* 結合的である

以上の性質に気づいたなら、モノイドに出会ったのである!

### Monoid型クラス
モノイドは、結合的な二項演算子と、その演算子に関する単位元からなる構造。Data.MonoidモジュールのMonoid型クラスの定義は以下のとおり
```haskell
class Monoid m where
    mempty :: m                     -- 単位元
    mappend :: m -> m -> m          -- 二項演算
    mconcat :: [m] -> m
    mconcat = foldr mappend mempty
```

### モノイド則
* `` mempty `mappend` x = x `` (単位元)
* `` x `mappend` mempty = x `` (単位元)
* `` (x `mappend` y) `mappend` z = x `mappend` (y `mappend` z) `` (結合的)
の三つ。

## 12.3 モノイドとの遭遇
### リストはモノイド
```haskell
instance Monoid [a] where
    mempty = []
    mappend = (++)
```
`mconcat`はデフォルト実装が指定される。

### ProductとSum
数は積と和のどちらを取ってもモノイドにすることが考えられる。そのようにある型について、同じ型クラスのインスタンスを複数定義したい時newtypeが使える。

